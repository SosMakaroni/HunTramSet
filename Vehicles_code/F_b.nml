// --------------------------------------------------------------------------------F_b

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_F_b_purchase, "src/F_b.png")			{tmpl_purchase()}
spriteset(ss_F_b_1_1cc_depo, "src/F_b.png")		{tmpl_depo(1,1)}
spriteset(ss_F_b_1_2cc_depo, "src/F_b.png")		{tmpl_depo(2,1)}
spriteset(ss_F_b_1_1cc, "src/F_b.png")			{tmpl_1(1,1,0,-3,-5,-11,-5,-7,-12,-3,-9,-3,-5,-13,-10,-7,-12,-5,-8)}
spriteset(ss_F_b_1_2cc, "src/F_b.png")			{tmpl_1(2,1,0,-3,-5,-11,-5,-7,-12,-3,-9,-3,-5,-13,-10,-7,-12,-5,-8)}
spriteset(ss_F_b_1_1cc_rak, "src/F_b.png")	{tmpl_1(1,1,1,-3,-5,-11,-5,-7,-12,-3,-9,-3,-5,-13,-10,-7,-12,-5,-8)}
spriteset(ss_F_b_1_2cc_rak, "src/F_b.png")	{tmpl_1(2,1,1,-3,-5,-11,-5,-7,-12,-3,-9,-3,-5,-13,-10,-7,-12,-5,-8)}


spritegroup sg_F_b_1_1cc {	loaded:  [ss_F_b_1_1cc];	loading: [ss_F_b_1_1cc_rak];}
spritegroup sg_F_b_1_2cc {	loaded:  [ss_F_b_1_2cc];	loading: [ss_F_b_1_2cc_rak];}

switch(FEAT_ROADVEHS, SELF, sw_F_b_1cc_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_F_b_1_1cc_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sg_F_b_1_1cc; // 0x00 Vehicle in game
}
switch(FEAT_ROADVEHS, SELF, sw_F_b_2cc_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_F_b_1_2cc_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sg_F_b_1_2cc; // 0x00 Vehicle in game
}
switch (FEAT_ROADVEHS, SELF, sw_F_b_ss, cargo_subtype) {
	1: sw_F_b_2cc_ss;
	sw_F_b_1cc_ss;
}
switch (FEAT_ROADVEHS, SELF, sw_F_b_capacity, position_in_consist) {
	return 42;
}
switch (FEAT_ROADVEHS, SELF, sw_F_b_length, position_in_consist) {
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_F_b_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_1CC);
	1: return string(STR_COLOUR_SCHEME_2CC);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_F_b) {
	property {
        name:								string(STR_F_b_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1941,01,01);
        model_life:						20;
        vehicle_life:						50;
        reliability_decay:				80;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					24;
        cost_factor:						20;
        running_cost_factor:				20;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								40 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								40 kW;
        weight:							10 ton;
        cargo_capacity:					42;
		cargo_age_period:					74;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw_F_b_ss;
		purchase:						ss_F_b_purchase;
		length:                  		sw_F_b_length;
		cargo_subtype_text: 			sw_F_b_livery_text;
		cargo_capacity: 				sw_F_b_capacity;
		purchase_cargo_capacity:		return 42;
		additional_text: 				string(STR_F_b_ADAT);
    }
}
// --------------------------------------------------------------------------------F_b End