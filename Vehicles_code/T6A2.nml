// --------------------------------------------------------------------------------T6A2

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_T6A2_purchase, "src/T6A2.png")			{tmpl_purchase()}
spriteset(ss_T6A2_1_1cc_depo, "src/T6A2.png")		{tmpl_depo(1,1)}
spriteset(ss_T6A2_1_2cc_depo, "src/T6A2.png")		{tmpl_depo(2,1)}
spriteset(ss_T6A2_1_1cc, "src/T6A2.png")			{tmpl_1(1,1,0,-3,-7,-18,-6,-14,-10,-12,-10,-3,-7,-12,-11,-14,-10,-6,-9)}
spriteset(ss_T6A2_1_2cc, "src/T6A2.png")			{tmpl_1(2,1,0,-3,-7,-18,-6,-14,-10,-12,-10,-3,-7,-12,-11,-14,-10,-6,-9)}
spriteset(ss_T6A2_1_1cc_rak, "src/T6A2.png")	{tmpl_1(1,1,1,-3,-7,-18,-6,-14,-10,-12,-10,-3,-7,-12,-11,-14,-10,-6,-9)}
spriteset(ss_T6A2_1_2cc_rak, "src/T6A2.png")	{tmpl_1(2,1,1,-3,-7,-18,-6,-14,-10,-12,-10,-3,-7,-12,-11,-14,-10,-6,-9)}


spritegroup sg_T6A2_1_1cc {	loaded:  [ss_T6A2_1_1cc];	loading: [ss_T6A2_1_1cc_rak];}
spritegroup sg_T6A2_1_2cc {	loaded:  [ss_T6A2_1_2cc];	loading: [ss_T6A2_1_2cc_rak];}

switch(FEAT_ROADVEHS, SELF, sw_T6A2_1cc_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_T6A2_1_1cc_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sg_T6A2_1_1cc; // 0x00 Vehicle in game
}
switch(FEAT_ROADVEHS, SELF, sw_T6A2_2cc_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_T6A2_1_2cc_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sg_T6A2_1_2cc; // 0x00 Vehicle in game
}
switch (FEAT_ROADVEHS, SELF, sw_T6A2_ss, cargo_subtype) {
	1: sw_T6A2_2cc_ss;
	sw_T6A2_1cc_ss;
}
switch (FEAT_ROADVEHS, SELF, sw_T6A2_capacity, position_in_consist) {
	return 101;
}
switch (FEAT_ROADVEHS, SELF, sw_T6A2_length, position_in_consist) {
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_T6A2_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_1CC);
	1: return string(STR_COLOUR_SCHEME_2CC);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_T6A2) {
	property {
        name:								string(STR_T6A2_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1997,05,08);
        model_life:						25;
        vehicle_life:						50;
        reliability_decay:				80;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					24;
        cost_factor:						20;
        running_cost_factor:				20;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								50 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								180 kW;
        weight:							18 ton;
        cargo_capacity:					101;
		cargo_age_period:					74;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw_T6A2_ss;
		purchase:						ss_T6A2_purchase;
		length:                  		sw_T6A2_length;
		cargo_subtype_text: 			sw_T6A2_livery_text;
		cargo_capacity: 				sw_T6A2_capacity;
		purchase_cargo_capacity:		return 101;
		additional_text: 				string(STR_T6A2_ADAT);
    }
}
// --------------------------------------------------------------------------------T6A2 End