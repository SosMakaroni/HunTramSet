// --------------------------------------------------------------------------------Ap_b

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_Ap_b_purchase, "src/Ap_b.png")			{tmpl_purchase()}
spriteset(ss_Ap_b_1_1cc_depo, "src/Ap_b.png")		{tmpl_depo(1,1)}
spriteset(ss_Ap_b_2_1cc_depo, "src/Ap_b.png")		{tmpl_depo(1,2)}
spriteset(ss_Ap_b_1_2cc_depo, "src/Ap_b.png")		{tmpl_depo(2,1)}
spriteset(ss_Ap_b_2_2cc_depo, "src/Ap_b.png")		{tmpl_depo(2,2)}
spriteset(ss_Ap_b_1_1cc, "src/Ap_b.png")			{tmpl_1(1,1,0,-3,-7,-11,-8,-7,-12,-3,-8,-3,-7,-13,-9,-7,-12,-5,-11)}
spriteset(ss_Ap_b_2_1cc, "src/Ap_b.png")			{tmpl_1(1,2,0,-3,-5,-11,-4,-5,-7,-1,-4,-3,-5,-13,-5,-5,-7,-3,-7)}
spriteset(ss_Ap_b_1_2cc, "src/Ap_b.png")			{tmpl_1(2,1,0,-3,-7,-11,-8,-7,-12,-3,-8,-3,-7,-13,-9,-7,-12,-5,-11)}
spriteset(ss_Ap_b_2_2cc, "src/Ap_b.png")			{tmpl_1(2,2,0,-3,-5,-11,-4,-5,-7,-1,-4,-3,-5,-13,-5,-5,-7,-3,-7)}
spriteset(ss_Ap_b_1_1cc_rak, "src/Ap_b.png")	{tmpl_1(1,1,1,-3,-7,-11,-8,-7,-12,-3,-8,-3,-7,-13,-9,-7,-12,-5,-11)}
spriteset(ss_Ap_b_2_1cc_rak, "src/Ap_b.png")	{tmpl_1(1,2,1,-3,-5,-11,-4,-5,-7,-1,-4,-3,-5,-13,-5,-5,-7,-3,-7)}
spriteset(ss_Ap_b_1_2cc_rak, "src/Ap_b.png")	{tmpl_1(2,1,1,-3,-7,-11,-8,-7,-12,-3,-8,-3,-7,-13,-9,-7,-12,-5,-11)}
spriteset(ss_Ap_b_2_2cc_rak, "src/Ap_b.png")	{tmpl_1(2,2,1,-3,-5,-11,-4,-5,-7,-1,-4,-3,-5,-13,-5,-5,-7,-3,-7)}


spritegroup sg_Ap_b_1_1cc {	loaded:  [ss_Ap_b_1_1cc];	loading: [ss_Ap_b_1_1cc_rak];}
spritegroup sg_Ap_b_2_1cc {	loaded:  [ss_Ap_b_2_1cc];	loading: [ss_Ap_b_2_1cc_rak];}
spritegroup sg_Ap_b_1_2cc {	loaded:  [ss_Ap_b_1_2cc];	loading: [ss_Ap_b_1_2cc_rak];}
spritegroup sg_Ap_b_2_2cc {	loaded:  [ss_Ap_b_2_2cc];	loading: [ss_Ap_b_2_2cc_rak];}

switch (FEAT_ROADVEHS, SELF, ss_Ap_b_1cc_depo, position_in_consist ) {
    0: ss_Ap_b_1_1cc_depo;
    ss_Ap_b_2_1cc_depo;
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_1cc, position_in_consist ) {
    1: sg_Ap_b_2_1cc;
    sg_Ap_b_1_1cc;
}
switch(FEAT_ROADVEHS, SELF, sw_Ap_b_1cc_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_Ap_b_1cc_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sw_Ap_b_1cc; // 0x00 Vehicle in game
}
switch (FEAT_ROADVEHS, SELF, ss_Ap_b_2cc_depo, position_in_consist ) {
    0: ss_Ap_b_1_2cc_depo;
    ss_Ap_b_2_2cc_depo;
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_2cc, position_in_consist ) {
    1: sg_Ap_b_2_2cc;
    sg_Ap_b_1_2cc;
}
switch(FEAT_ROADVEHS, SELF, sw_Ap_b_2cc_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_Ap_b_2cc_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sw_Ap_b_2cc; // 0x00 Vehicle in game
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_ss, cargo_subtype) {
	1: sw_Ap_b_2cc_ss;
	sw_Ap_b_1cc_ss;
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_articulated, extra_callback_info1) {
    0..1: item_Ap_b;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_capacity, position_in_consist) {
	1: return 34;
	return 28;
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_length, position_in_consist) {
	1: return 8;
    return 6;
}
switch (FEAT_ROADVEHS, SELF, sw_Ap_b_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_1CC);
	1: return string(STR_COLOUR_SCHEME_2CC);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_Ap_b) {
	property {
        name:								string(STR_Ap_b_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1901,01,01);
        model_life:						13;
        vehicle_life:						48;
        reliability_decay:				80;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					48;
        cost_factor:						20;
        running_cost_factor:				20;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								35 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								60 hp;
        weight:							12 ton;
        cargo_capacity:					62;
		cargo_age_period:					74;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw_Ap_b_ss;
		purchase:						ss_Ap_b_purchase;
		articulated_part:        		sw_Ap_b_articulated;
		length:                  		sw_Ap_b_length;
		cargo_subtype_text: 			sw_Ap_b_livery_text;
		cargo_capacity: 				sw_Ap_b_capacity;
		purchase_cargo_capacity:		return 62 / 2;
		additional_text: 				string(STR_Ap_b_ADAT);
    }
}
// --------------------------------------------------------------------------------Ap_b End